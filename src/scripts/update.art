;========================================================
; Arturo
; Programming Language + Bytecode VM compiler
; (c) 2019-2023 Yanis Zafirópulos
;
; @file: system/update.art
;========================================================

; TODO(Script\update) needs complete rewrite
;  currently the updater barely works, we should re-think and re-design the whole process and functionality
;  labels: installer,open discussion,enhancement,bug,critical

; » Helper functions

inform: $[content :string] -> 
	print [ color #green content]


; » Main functions

lookForReleases: $[][
	inform "- Checking Arturo repository..."
	data: read.json "https://api.github.com/repos/arturo-lang/nightly/releases/latest"
	inform ~"- Latest version found: |data\body|"

	return data
]

downloadLatestRelease: $[
	data :dictionary 
	asset :integer
][
	inform "- Downloading new version..."
	download.as:"art.tar.gz"  (data\assets \ asset) \ "browser_download_url"
]

extractDownload: $[][
	inform "- Extracting..."
	execute "tar xvfz art.tar.gz -C ~/.arturo/bin"
]

cleanupDownload: $[][
	inform 	"- Cleaning up..."
	execute "rm -rf art.tar.gz"
	inform 	"- Done. :)"
]

acceptance: $[][
	message: "  ** Are you sure you want to install it? [y/n]: "
	confirm: input message | 
						   | lower 
						   | strip
			
	return in? confirm ["y" "yes"]

]


main: $[][
	
	data: lookForReleases
	
	if? not? acceptance -> exit

	asset: ø
	case [sys\os]
		when? [="macosx"]-> asset: 2
		when? [="linux"]-> asset: 0
		else [
			panic ~{your operating system (|sys\os|) doesn't support self-updates}
		]
		
	downloadLatestRelease data asset
	extractDownload
	cleanupDownload
	
]

main
