;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Arturo
;; Programming Language + Bytecode VM compiler
;; (c) 2019-2021 Yanis ZafirÃ³pulos
;;
;; @file: system/console.art
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; helpers & templates

LongHeader: {
	|color.green.bold "Arturo"|  
	|sys\copyright| |sys\author|
}

Details: {
	# v/|sys\version| b/|sys\build| @ |sys\buildDate| 
	# arch: |sys\cpu|/|sys\os|
}

BR: "\n"

orange: $[x][
	color.rgb:208 x
]

orangeBold: $[x][
	color.bold.rgb:208 x
]

;; print header

clear
print join @[
	~LongHeader BR BR
	color.gray ~Details BR BR
	orange "# Type " 
	orangeBold "info 'symbol"
	orange " for info about a specific symbol or built-in function" BR
	orange "# Type " 
	orangeBold "help"
	orange " to get a list of all available functions with a short description" BR
	orange "# For multi-line input, just add a blank space at the end of a line" BR BR
	orange "# Launching interactive console - rock on! ðŸ¤˜" BR
]

;; start the REPL

while [true] .import [
	;push null
	Got: null

	indent: ""
	
	inp: input "$> " 
	while [suffix? inp " "][
		if suffix? strip inp "[" [
			indent: indent ++ "\t"
		]
		inp: inp ++ input ".. " ++ indent
	]

	try.verbose.import [
		try.verbose.import to :block inp

		StackSize: size stack
		
		if StackSize > 0 		-> Got: pop 1
		if Got <> null 			-> print color.gray ~"=> |Got|"

		print ""
	]
]
