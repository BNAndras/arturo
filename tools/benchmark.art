#!/usr/bin/env arturo

Compilers: ["arturo"] ++ (key? args 'with)? -> split.by:"," args\with
                                            -> "art2"
LookUp: args\values\0

BenchmarkFile: "benchmark_results.json"
Results: new []

getList: function [pa][
    pattern: to :regex ".*" ++ LookUp ++ ".*\.art"
    result: select list.recursive pa 'x -> contains? x pattern
    (contains? pa "examples")? [
        return select result 're -> exists? replace re ".art" ".res"
    ] -> return result
]

showHeader: function [id, total, tst][
    print ""
    print repeat "=" 120
    print [color #green ~"[|id+1|/|total|]" tst]
    print repeat "=" 120
    print ""
]

processPartialResults: function [data][
    basis: data\results\0\mean
    dic: #[]
    loop slice data\results 1 dec size data\results 'item [
        dic\[item\parameters\compiler]: basis // item\mean
    ]
    'Results ++ dic
]

sanitizeResults: function [num][
    compilers: keys Results\0
    final: []
    dicc: #[]
    loop compilers 'comp -> dicc\[comp]: 0.0

    loop Results 'res [
        loop res [k,v][
            dicc\[k]: dicc\[k] + v
        ]
    ]

    loop compilers 'comp [
        final: final ++ #[
            compiler: comp,
            speed: dicc\[comp] // num
        ]
    ]
    return sort.descending.by:'speed final
]

presentResults: function [results][
    topResult: first results
    metric: topResult\speed
    verb: (metric < 1.0)? -> "⬇️  decreased" -> "⬆️  increased"
    if metric > 1.0 -> metric: metric - 1.0
    metric: metric * 100.0
    print ""
    print color.bold #cyan repeat "=" 120
    print (color.bold #cyan "| ") ++ color.bold #yellow "Best compiler"
    print [(color.bold #cyan "| ") ++ "\t" topResult\compiler]
    print (color.bold #cyan "| ") ++ color.bold #yellow "Performance"
    print [(color.bold #cyan "| ") ++ "\t" verb "by approximately" ~"|to :string .format:{.2f} metric|%"]
    print color.bold #cyan repeat "=" 120
    print ""
]

tests: (getList "../benchmarks/tests") ++
       (getList "examples")

total: size tests
loop.with:'i tests 'test [
    showHeader i total test
    scr: replace escape.shell test {"} {\"}
    execute.directly ~{!sh
        hyperfine -L compiler |join.with:"," Compilers| --export-json |BenchmarkFile| "{compiler} |scr|"
    }
    processPartialResults read.json BenchmarkFile
]

sanitizedResults: sanitizeResults total
presentResults sanitizedResults

delete BenchmarkFile