#!/usr/bin/env arturo
;==========================================
; Tester
;
; @file: tools/tester.art
; @author: drkameleon
;==========================================

runSet: function [title,location][
    print color.bold.cyan ~"\n> |title|"
    tests: sort select list relative location => [in? ".res"]
    success: 0

    loop tests 'test [
        script: replace test ".res" ".art"
        expected: split.lines strip read test

        prints pad.right ((color.magenta "\n- Running: ") ++ truncate.preserve extract.filename script 40) 70 
        result: split.lines strip execute ~{arturo "|script|"}

        if? result=expected [
            prints color.green.bold "[ OK ]"
            success: success + 1
        ]
        else [
            print "<-------------------"
            print result
            print "------------------->"
            prints color.red "[ Error ]"
        ]
    ]

    print "\n"
    print render {
        ******************************
        * |success| of |size tests|
        * tasks succeeded.
        ******************************
    }

    if success < size tests [ 
        panic.code: 1 "testing failed"
    ]
]

print color.bold {
    |============================================
    | Arturo Tester
    |============================================
}

runSet "Unit Tests" "../tests"
runSet "Examples" "../examples/rosetta"

exit